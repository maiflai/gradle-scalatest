buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.2"
    }
}
plugins {
    id "com.jfrog.bintray" version "1.0"
}

apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'signing'
apply plugin: 'maven'
apply plugin: 'groovy'

group = 'com.github.maiflai'
description = 'gradle-scalatest is a Gradle plugin for executing scalatest tests'
ext {
    websiteUrl = 'https://github.com/maiflai/gradle-scalatest'
    sourceUrl = 'https://github.com/maiflai/gradle-scalatest.git'
}

targetCompatibility = '1.6'

repositories {
    mavenCentral()
}

//noinspection GrUnresolvedAccess,GroovyAssignabilityCheck
task createClasspathManifest {
    def outputDir = sourceSets.test.output.resourcesDir

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        new File(outputDir, 'plugin-classpath.txt').text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testCompile gradleTestKit()
    testCompile files(createClasspathManifest)
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    from "$buildDir/docs/groovydoc"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

test.dependsOn(jar)

artifacts {
    archives jar
    archives groovydocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

if (project.version == 'unspecified') {
    version = '0.1-SNAPSHOT'
    project.signArchives.enabled = false
}

task showVersion() { doLast { println(project.version) } }



def propOrDefault(String property) {
    if (project.hasProperty(property)) {
        return project.getProperty(property)
    } else {
        return ''
    }
}

pluginBundle {
    website = project.websiteUrl
    vcsUrl = project.sourceUrl
    description = project.description
    tags = ['testing', 'scala', 'scalatest']

    plugins {
        scalatestPlugin {
            id = 'com.github.maiflai.scalatest'
            displayName = 'Gradle ScalaTest plugin'
        }
    }
}

bintray {
    user = propOrDefault('bintrayUsername')
    key = propOrDefault('bintrayApiKey')
    configurations = ['archives']
    dryRun = false
    publish = false
    pkg {
        repo = 'maven'
        name = project.name
        desc = project.description
        websiteUrl = project.websiteUrl
        issueTrackerUrl = 'https://github.com/maiflai/gradle-scalatest/issues'
        vcsUrl = project.sourceUrl
        licenses = ['unlicense']
        labels = ['gradle', 'testing', 'scala']
        publicDownloadNumbers = true
        version {
            attributes = ['gradle-plugin': 'com.github.maiflai.scalatest:com.github.maiflai:gradle-scalatest']
        }

    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: propOrDefault('sonatypeUsername'), password: propOrDefault('sonatypePassword'))
            }

            pom.project {
                name project.name
                description project.description
                url project.websiteUrl

                scm {
                    url "scm:git:${project.sourceUrl}"
                }

                licenses {
                    license {
                        name 'unlicense'
                        url 'http://unlicense.org'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'maiflai'
                    }
                }
            }
        }
    }
}
